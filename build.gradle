buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.google.protobuf:protobuf-gradle-plugin:0.7.6"
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'com.google.protobuf'

group = 'com.github.ppoffice'

repositories {
    mavenCentral()
}

[ compileJava, compileTestJava ]*.options*.encoding = 'UTF-8'

sourceCompatibility='1.7'
targetCompatibility='1.7'

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0-beta-1'
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                javalite { }
            }
        }
    }
    generatedFilesBaseDir = "$projectDir/src"
}

dependencies {
    compile 'com.google.protobuf:protobuf-lite:3.0.0'
    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// To specify a license in the pom:
install {
    repositories.mavenInstaller {
        pom.project {
            licenses {
                license {
                    name 'GNU General Public License v3.0'
                    url 'http://www.gnu.org/licenses/gpl-3.0.txt'
                    distribution 'repo'
                }
            }
        }
    }
}